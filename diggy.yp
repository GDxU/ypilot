use "standard:dropped.yp"
use "standard:active.yp"

## digging ##

a Diggy thing has
  a hardness which is a number (default 0)

a Pick is Diggy # diggy diggy hole!

when ?pick is added and ?pick is a Pick
then
  let ?tri be [Vec2[-12, -12], Vec2[0, 12], Vec2[12, -12]]
  ?pick becomes
    Tangible with shape ?tri
    Visible with graphics [
      <polygon points="?tri" stroke="#4e7cfe" />,
      <path d="M 6,2 L -4,-8 M -7,0 A 9,9, 0, 0, 1, 4,-11" stroke="#fe3a27" />
    ]

a Diggable thing is Tangible and has
  # what level of pick is necessary to dig this?
  a hardness which is a number (default 0)
  # 0 => placed; (0,1) => partly dug; 1 => dropped
  a dugness which is a number (default 0)
  # a visible thing representing dugness
  a crack (default nothing)

a Block is Diggable

a Crack is Visible

# when a block starts being dug, add a visible crack
when ?block becomes Diggable with dugness ?d and crack nothing and
  (?d > 0)
  (?d <= 1)
  ?block is Located with space ?space and position ?pos
  there is a thing ?map which is
    Mapped with blockSize ?bs
    Located with space ?space
then
  let ?edgeX be (?bs["x"] / 2)
  let ?edgeY be (?bs["y"] / 2)
  let ?tipX be (?edgeX * (1 - ?d))
  let ?tipY be (?edgeY * (1 - ?d))
  a new Crack ?crack is added which is
    Located with space ?space and position ?pos
    Visible with graphics [
      <g>
       <path transform="translate(?edgeX,?edgeY)"
             d="M ?edgeX,?edgeY L ?tipX,?tipY M ?edgeX,-?edgeY L ?tipX,-?tipY M -?edgeX,?edgeY L -?tipX,?tipY M -?edgeX,-?edgeY L -?tipX,-?tipY"
	     stroke="#fe3a27" />
      </g>
    ]
  ?block becomes Diggable with crack ?crack

# when a block's dugness is changed, update its crack
when ?block becomes Diggable with dugness ?d and crack ?crack and
  (?d > 0)
  (?d <= 1)
  ?crack is Visible with graphics ?g
  ?block is Located with space ?space and position ?pos
  there is a thing ?map which is
    Mapped with blockSize ?bs
    Located with space ?space
then
  let ?edgeX be (?bs["x"] / 2)
  let ?edgeY be (?bs["y"] / 2)
  let ?tipX be (?edgeX * (1 - ?d))
  let ?tipY be (?edgeY * (1 - ?d))
  ?crack becomes Visible with graphics [
    <g>
     <path transform="translate(?edgeX,?edgeY)"
           d="M ?edgeX,?edgeY L ?tipX,?tipY M ?edgeX,-?edgeY L ?tipX,-?tipY M -?edgeX,?edgeY L -?tipX,?tipY M -?edgeX,-?edgeY L -?tipX,-?tipY"
	   stroke="#fe3a27" />
    </g>
  ]

# when a cracked block becomes not dug at all, remove its crack
when ?block becomes Diggable with dugness 0 and crack ?crack and
  (?crack != nothing)
then
  ?block becomes Diggable with crack nothing
  ?crack is removed

# when a cracked block leaves the space, remove its crack
when ?block becomes not Located and
  ?block is Diggable with crack ?crack
  (?crack != nothing)
then
  ?block becomes Diggable with crack nothing
  ?crack is removed

# TODO move crack when block moves? shouldn't happen

a Digging thing is Diggy and has
  a block which is a Diggable thing

when ?pick becomes Active with ship ?ship and
  ?pick is Diggy
  ?ship is Located with space ?space and position ?shipPos
  there is a thing ?map which is
    Mapped with blockSize ?bs
    Located with space ?space
  ?ship is Oriented with orientation ?a
  let ?front be (?shipPos + Vec2[cos(?a), sin(?a)] * ?bs)
  let ?blockPos be (?front - (?front % ?bs))
  # TODO special case this search to use SpatialIndex for speed
  there is a thing ?block which is
    Located with space ?space and position ?blockPos
    Diggable
then ?pick becomes Digging with block ?block

when ?pick becomes not Active and ?pick is Digging
then ?pick becomes not Digging

in the event ?pick DigsAt ?block then
  ?pick is a Digging thing
  ?block is a Diggable thing

when the clock ticks and
  there is a thing ?pick which is
    Digging with block ?block
then ?pick DigsAt ?block

only allow the event ?pick DigsAt ?block when
  ?pick is Diggy with hardness ?pickHardness
  ?block is Diggable with hardness ?blockHardness
  (?pickHardness >= ?blockHardness)

when ?pick DigsAt ?block and
  ?block is Diggable with dugness ?dugness
  (?dugness < 1)
then
  # dig 1/20 per tick so that we dig a whole block in one second
  # TODO adjust this ratio according to hardness difference?
  ?block becomes Diggable with dugness (?dugness + 1/20)

# TODO heal partly dug blocks when they're not actively being dug?

in the event ?pick DigsUp ?block then
  ?pick is a Diggy thing
  ?block is a Diggable thing

when ?pick DigsAt ?block and
  ?block is Diggable with dugness ?dugness
  (?dugness >= 1)
then
  ?pick becomes not Digging
  ?block becomes Diggable with dugness 1 # clamp to 1 so Crack works
  ?pick DigsUp ?block

when ?pick DigsUp ?block and
  ?block is Located with space ?space and position ?pos
then ?block Drops in ?space at ?pos with velocity Vec2[0,0]

## placement ##

a TryingToBePlaced thing has
  a tryingToBePlaced flag (default true)

# activating a diggable block drops it while TryingToBePlaced
when ?block becomes Active with ship ?ship and
  ?block is Diggable
  ?block is Held with holder ?slot
then
  ?block becomes TryingToBePlaced
  ?slot IsToDrop from ?ship

in the event ?block IsPlaced in ?space at ?position then
  ?block is a Diggable thing
  ?position is a Vec2 object
  ?space is a Spatial thing

# when a block drops while TryingToBePlaced, it's placed
when ?block Drops in ?space at ?pos with velocity ?v and
  ?block is TryingToBePlaced
  there is a thing ?map which is
    Mapped with blockSize ?bs
    Located with space ?space
then
  ?block becomes not TryingToBePlaced
  let ?placedPos be (?pos - (?pos % ?bs))
  ?block IsPlaced in ?space at ?placedPos

# blocks may only be placed where there isn't already a block
disallow the event ?block IsPlaced in ?space at ?position when
  there is a thing ?otherBlock which is
    Located with space ?space and position ?position

# when a block is placed it becomes located and diggable again
when ?block IsPlaced in ?space at ?position
then
  ?block becomes
    Located with space ?space and position ?position
    Diggable with dugness 0
    not Dropped
    not Fleeting
    not Mobile

# TODO
# - standard graphical transformation for Visible blocks: make it smaller and surround it with a blue triangle
# - standard graphics for dugness
