use "standard:bullet.yp"
use "standard:active.yp"
use "standard:owned.yp"

a Repeating thing has
  a period which is a number # number of ticks between firing
  a timeLeft which is a number (default 0) # number of ticks until next firing

a Shooty thing has
  # really a speed, but "velocity" is conventional for this
  a muzzleVelocity which is a number (default 1)
  a bulletHP which is a number (default 1)
  a bulletDuration which is a number (default 30)

a Gun is Repeating and Shooty

when ?gun is added and ?gun is a Gun
then
  let ?tri be [Vec2[-12, -12], Vec2[0, 12], Vec2[12, -12]]
  ?gun becomes
    Tangible with shape ?tri
    Visible with graphics [
      <polygon points="?tri" stroke="#4e7cfe" />,
      <path d="M 6,-6.01 A 7.5,3, 0, 1, 0, 6,-6 L 10,-6 M 6,-10 L 6,-2 M -4,-3 L -6,4 L -3,4 L -1,-3 L 0,0" stroke="#fe3a27" />
    ]

in the event ?gun Fires from ?ship then
  ?gun is a Shooty thing
  ?ship is a Located thing

# when a repeating thing with no time left becomes active
when ?x becomes Active with ship ?s and
  ?x is Repeating with period ?p and timeLeft ?t
  (?t == 0)
then
  ?x becomes Repeating with timeLeft ?p
  ?x Fires from ?s

# when an active thing runs out of time left
when ?x becomes Repeating with period ?p and timeLeft ?t and
  (?t == 0)
  ?x is Active with ship ?s
then
  ?x becomes Repeating with timeLeft ?p
  ?x Fires from ?s

# time left ticks down to 0 (even when not active)
when the clock ticks and
  there is a thing ?r which is Repeating with timeLeft ?t
  (?t > 0)
then
  ?r becomes Repeating with timeLeft (?t - 1)

when ?gun Fires from ?ship and
  ?gun is Shooty with
    muzzleVelocity ?bulletSpeed and
    bulletHP ?hp and
    bulletDuration ?ttl
  ?ship is Located with space ?space and position ?shipPos
  ?ship is Oriented with orientation ?shipAngle
  ?ship is Mobile with velocity ?shipVel # TODO angularVelocity?
  ?ship is Owned with owner ?owner
then
  let ?forward be Vec2[cos(?shipAngle), sin(?shipAngle)]
  let ?bulletPos be (?shipPos + ?forward * 16) # FIXME depends on ship size
  let ?bulletVel be (?shipVel + ?forward * ?bulletSpeed)
  a new Bullet ?bullet is added which is
    Damaging with hitPoints ?hp
    Fleeting with timeToLive ?ttl
    Located with space ?space and position ?bulletPos
    Mobile with velocity ?bulletVel
    Tangible with shape [Vec2[-0.5,-0.5], Vec2[0.5,-0.5], Vec2[0.5,0.5], Vec2[-0.5,0.5]]
    Visible with graphics [<circle r="0.5" fill="white" />]
    Owned with owner ?owner

