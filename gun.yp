use "standard:bullet.yp"
use "standard:controls.yp" # for Active

a Repeating thing has
  a period which is a number # number of ticks between firing
  a timeLeft which is a number (default 0) # number of ticks until next firing

a Firing thing has
  a ship which is a BasicShip

# Firing is only for one tick
when ?x becomes Firing
then ?x becomes not Firing

# when a repeating thing with no time left becomes active
when ?x becomes Active with ship ?s and
  ?x is Repeating with period ?p and timeLeft ?t
  (?t == 0)
then
  ?x becomes
    Firing with ship ?s
    Repeating with timeLeft ?p

# when an active thing runs out of time left
when ?x becomes Repeating with period ?p and timeLeft ?t and
  (?t == 0)
  ?x is Active with ship ?s
then
  ?x becomes
    Firing with ship ?s
    Repeating with timeLeft ?p

# time left ticks down to 0 (even when not active)
when the clock ticks and
  there is a thing ?r which is Repeating with timeLeft ?t
  (?t > 0)
then
  ?r becomes Repeating with timeLeft (?t - 1)

a Shooty thing has
  # really a speed, but "velocity" is conventional for this
  a muzzleVelocity which is a number (default 1)
  a bulletHP which is a number (default 1)
  a bulletDuration which is a number (default 30)

a Gun is Repeating and Shooty

when ?gun becomes Firing with ship ?ship and
  ?gun is Shooty with
    muzzleVelocity ?bulletSpeed and
    bulletHP ?hp and
    bulletDuration ?ttl
  ?ship is Located with space ?space and position ?shipPos
  ?ship is Oriented with orientation ?shipAngle
  ?ship is Mobile with velocity ?shipVel # TODO angularVelocity?
then
  let ?forward be Vec2[cos(?shipAngle), sin(?shipAngle)]
  let ?bulletPos be (?shipPos + ?forward * 16) # FIXME depends on ship size
  let ?bulletVel be (?shipVel + ?forward * ?bulletSpeed)
  a new Bullet ?bullet is added which is
    Damaging with hitPoints ?hp
    Fleeting with timeToLive ?ttl
    Located with space ?space and position ?bulletPos
    Mobile with velocity ?bulletVel
    Solid with shape [Vec2[-0.5,-0.5], Vec2[0.5,-0.5], Vec2[0.5,0.5], Vec2[-0.5,0.5]]
    Visible with graphics [<circle r="0.5" fill="white" />]

